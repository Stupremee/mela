plugins {
  id 'java'
  id 'org.jetbrains.kotlin.jvm' version '1.3.21'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
  id 'checkstyle'
  id 'pmd'
  id 'com.github.spotbugs' version '1.7.1'
}

group 'com.github.stupremee.mela'
version '0.1.0'

sourceCompatibility = 11
targetCompatibility = 11

ext {
  kotlin_version = "1.3.21"
  logback_version = "1.2.3"
  immutables_version = "2.7.4"
  junit_version = "4.12"
  vavr_version = "0.9.3"
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  // D4J for wrapping the DiscordAPI
  implementation 'com.discord4j:discord4j-core:3.0.1'

  // Vavr for classes like Option, Try and many more useful stuff
  implementation "io.vavr:vavr:$vavr_version"

  // Cava-Toml to parse Toml files
  implementation "me.grison:jtoml:1.0.0"

  // Logback as slf4j implementation
  implementation "ch.qos.logback:logback-core:$logback_version"
  implementation "ch.qos.logback:logback-classic:$logback_version"

  // Immutables
  implementation "org.immutables:value:$immutables_version"
  annotationProcessor "org.immutables:value:$immutables_version"

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

  //                                           //
  // ----------- Test Dependencies ----------- //
  //                                           //
  testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

  testCompile "junit:junit:$junit_version"
}

sourceSets {
  main.kotlin.srcDirs += 'src/main/kotlin'
  main.java.srcDirs += 'src/main/java'
}

checkstyle {
  checkstyleTest.enabled = false
  toolVersion = '8.18'
}

checkstyleMain {
  configFile = new File("$rootDir/config/checkstyle/google_checks.xml")
}

spotbugs {
  spotbugsTest.enabled = false
  toolVersion = '3.1.12'
  ignoreFailures = true
}

spotbugsMain {
  reports {
    html.enabled = true
    xml.enabled = false
  }
}

pmd {
  pmdTest.enabled = false
}

pmdMain {
  ignoreFailures = true
  ruleSets = ["category/java/errorprone.xml", "category/java/bestpractices.xml", "category/java/design.xml"]
}

wrapper {
  gradleVersion = "5.2.1"
  distributionType = Wrapper.DistributionType.ALL
}

compileKotlin {
  kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
  kotlinOptions.jvmTarget = "11"
}

compileJava.options.encoding = 'UTF-8'